plugins {
    id 'kotlin'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            groupId 'com.github.funfunfunny'
            artifactId 'ClassGuard'
            version "1.0.1"
        }
    }
    repositories {
        maven {
            // 生成的插件位置
            url = uri('../repo')
        }
    }
}

sourceSets {
    main.java.srcDirs += "$buildDir/generated/sources/java-templates/java/main"
}

compileKotlin {
    dependsOn 'copyJavaTemplates'
    kotlinOptions.freeCompilerArgs = ["-Xallow-result-return-type"]
}

task copyJavaTemplates(type: Copy) {
    from 'src/main/java-templates'
    into "$buildDir/generated/sources/java-templates/java/main"
    expand('projectVersion': "1.0.1")
    filteringCharset = 'UTF-8'
}

dependencies {
    compileOnly gradleApi()
    api 'org.codehaus.groovy:groovy-xml:3.0.9'
    api 'com.squareup:javapoet:1.13.0'
    compileOnly 'com.android.tools.build:gradle:7.3.1'
}